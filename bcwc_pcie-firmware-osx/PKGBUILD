# Maintainer: Maurizio D'Addona <mauritiusdadd@gmail.com>

# NOTE: In order to build this package, you need the firmware for the camera
#       See the archwiki for more information on how to obtain it.

pkgname="bcwc_pcie-firmware-osx"
pkgver=1.38
pkgrel=1
pkgdesc="OS X firmware for the reverse engineered Linux driver for the Broadcom 1570 PCIe webcam"
url="https://github.com/patjak/bcwc_pcie"
makedepends=('git' 'gzip')
arch=('i686' 'x86_64')
provides=("bcwc_pcie-firmware")
conflicts=("bcwc_pcie-firmware")
license=('custom')

source=("extraction-tool::git+https://gist.github.com/ea82507d46f0d77fb922.git"
        "AppleCameraInterface")

sha256sums=('SKIP'
            '4667e6828f6bfc690a39cf9d561369a525f44394f48d0a98d750931b2f3f278b')

prepare()
{
  chmod +x "${srcdir}/extraction-tool/extract-firmware.sh"
}

build()
{
  cd "${srcdir}/extraction-tool"
  ./extract-firmware.sh "${srcdir}/AppleCameraInterface"
}

package()
{
  cd "${srcdir}/extraction-tool"
  install -dm755 "$pkgdir/usr/lib/firmware/facetimehd/"
  install -m644 'firmware.bin' "$pkgdir/usr/lib/firmware/facetimehd"
}
