# Maintainer: Maurizio D'Addona <mauritiusdadd@gmail.com>
# Contributor: Xyne

# http://git.salome-platform.org/gitweb/

_modulename="med"

_gitname="${_modulename}"
_gitbranch="V7_6_BR" 
#_gitbranch="master"

_basename="salome-${_modulename}"

pkgname="salome-${_modulename}-git"
pkgver=7.6.0b1.r0.ge95c248
pkgrel=1
pkgdesc="Generic platform for Pre and Post-Processing for numerical simulation (MED Module)"
url="http://www.salome-platform.org"
depends=('salome-gui-git' 'med-salome' 'parmetis3')
makedepends=('git' 'doxygen' 'python2-sphinx' 'python2-docutils')
options=(!strip staticlibs emptydirs libtool docs)
arch=('i686' 'x86_64')
conflicts=('salome-med')
provides=('salome-med')
license=('LGPL')

source=("${pkgname}::git://git.salome-platform.org/modules/${_gitname}.git#branch=${_gitbranch}" )

sha256sums=('SKIP')


_prefix="/opt/salome-platform/${_modulename}"
_builddir="${pkgname}/build"

pkgver()
{
  cd "${srcdir}/${pkgname}"
  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g;s/^[vV]//g;s/_main_/./g;s/_/./g'
  #printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare()
{
  rm -rf "${srcdir}/${_builddir}"
  mkdir -p "${srcdir}/${_builddir}"

  cd "${srcdir}/${pkgname}"

  msg "Patching for python2 interpreter 1/2..."
  find . -type f \
    -execdir /usr/bin/sed\
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|g"\
      -e "s|#![ ]*/usr/bin/env python[ ]|#!/usr/bin/env python2 |g"\
      -i "{}"\
  \;

  msg "Patching for python2 interpreter 2/2..."
  for _prog in "python" "sphinx-build" "sphinx-apidoc"; do
      find . -type f -not  \(\
        -name "*.h"   -or\
        -name "*.hxx" -or\
        -name "*.hpp" -or\
        -name "*.hh"  -or\
        -name "*.c"   -or\
        -name "*.cxx" -or\
        -name "*.cpp" -or\
        -name "*.cc"  -or\
        -name "*.txt" \)\
        -execdir /usr/bin/sed\
          -e "s/^${_prog}$/${_prog}2/g"\
          -e "s/^${_prog} /${_prog}2 /g"\
          -e "s/\([ =]\)${_prog} /\1${_prog}2 /g"i\
          -i "{}"\
        \;
  done
}

build()
{
  source /etc/profile.d/salome-kernel.sh
  source /etc/profile.d/salome-gui.sh

  cd "${srcdir}/${_builddir}"

  _flags="-fPIC"

  cmake "${srcdir}/${pkgname}" \
    -DPREFIX=${_prefix} \
    -DCMAKE_INSTALL_PREFIX="${pkgdir}/${_prefix}" \
    -DCMAKE_DESTDIR="${pkgdir}" \
    -DCMAKE_C_COMPILER=mpicc \
    -DCMAKE_CXX_COMPILER=mpicxx \
    -DCMAKE_C_FLAGS="${CFLAGS} ${_flags} -D_DEFAULT_SOURCE"\
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} ${_flags} -D_DEFAULT_SOURCE"\
    -DPYTHON_VERSION=2.7 \
    -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
    -DPYTHON_LIB=/usr/lib/libpython2.7.so \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DSPHINX_EXECUTABLE=/usr/bin/sphinx-build2 \
    -DPYQT_PYUIC_EXECUTABLE=/usr/bin/python2-pyuic4 \
    -DPYQT_PYRCC_EXECUTABLE=/usr/bin/pyrcc4 \
    -DMPI_LIB_mpi=/usr/lib/openmpi/libmpi.so \
    -DMPI_LIB_mpi_cxx=/usr/lib/openmpi/libmpi_cxx.so \
    -DOMNIORBPY_ROOT_DIR=/usr/lib/python2.7/site-packages/omniORB \
    -DBOOST_ROOT_DIR=/usr \
    -DPYQT4_ROOT_DIR=/usr \
    -DPARAVIEW_ROOT_DIR=/usr \
    -DQWT_ROOT_DIR=/usr \
    -DQWT_LIBRARY="/usr/lib/qwt/libqwt.so" \
    -DCAS_ROOT_DIR=/opt/opencascade \
    -DQT4_ROOT_DIR=/usr \
    -DOPENGL_ROOT_DIR=/usr \
    -DVTK_ROOT_DIR=/usr/ \
    -DCAS_INCLUDE_DIRS=/opt/opencascade/inc \
    -DLIBXML2_INCLUDE_DIR=/usr/include/libxml2 \
    -DMETIS_INCLUDE_DIRS="/usr/include/metis-4" \
    -DMETIS_LIBRARIES="/usr/lib/libmetis-4.so" \
    -DPARMETIS_LIBRARIES=/usr/lib/libparmetis-3.so \
    -DPARMETIS_INCLUDE_DIRS=/usr/include/parmetis-3 \
    -DPARMETIS_SEQ_LIBRARIES=/usr/lib/libmetis-4.so \
    -DSALOME_BUILD_DOC=ON \
    -DSALOME_BUILD_TESTS=ON \
    -DSALOME_USE_MPI=ON \
    -DSALOME_CMAKE_DEBUG=ON \
    -DCMAKE_BUILD_TYPE='Debug' \
    -Wno-dev

    make ${MAKEFLAGS}

    msg "Generating environment variables..."

    echo "export MED_ROOT_DIR=${_prefix}" > ${_basename}.sh
    echo "export PATH=\${PATH}:${_prefix}/bin/salome" >> ${_basename}.sh
    echo "export PYTHONPATH=\${MED_ROOT_DIR}/lib/python2.7/site-packages/salome:\${PYTHONPATH}" >> ${_basename}.sh
    echo "${_prefix}/lib/" > ${_basename}.conf
    echo "${_prefix}/lib/salome" >> ${_basename}.conf
}

package()
{
  #needed to correctly loading modules during installation process
  _pythondir="${pkgdir}/${_prefix}/lib/python2.7/site-packages/salome"
  mkdir -p "${_pythondir}/salome"
  touch "${_pythondir}/salome/__init__.py"
  export PYTHONPATH="${_pythondir}"
  source /etc/profile.d/salome-kernel.sh
  source /etc/profile.d/salome-gui.sh
  cd "${srcdir}/${_builddir}"

  # NOTE: destdir="..." does not work!
  make install

  install -Dm755 "./${_basename}.sh" "${pkgdir}/etc/profile.d/${_basename}.sh"
  install -Dm755 "./${_basename}.conf" "${pkgdir}/etc/ld.so.conf.d/${_basename}.conf"

  rm -f "${pkgdir}/${_prefix}/adm_local/unix/config_files/check_GUI.m4"
  mv -f "${pkgdir}/${_prefix}/bin/salome/VERSION" "${pkgdir}/${_prefix}/bin/salome/VERSION-${_basename}"

  # Add missing __init__.py file to ensure that the modules are detected.
  find "$pkgdir/${_prefix}/lib/python2.7/site-packages/salome" -type d -exec touch '{}'/__init__.py \;

  #replacing the annoying hardcoded paths...
  msg "Fixing hardcoded paths..."
  find ${pkgdir} \
    -type f \( \
      -name "*.py" -o  \
      -name "*.txt" -o \
      -name "*.cmake"  \
    \) \
    -execdir /usr/bin/sed \
      -e "s|${srcdir}/${_builddir}|${_prefix}|g" \
      -e "s|${srcdir}|${_prefix}|g" \
      -e "s|${pkgdir}|${_prefix}|g" \
      -i '{}' \
  \;
}

